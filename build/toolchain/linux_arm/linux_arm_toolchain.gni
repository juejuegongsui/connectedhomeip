# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")

import("${build_root}/config/android/config.gni")
import("${build_root}/toolchain/gcc_toolchain.gni")

declare_args() {
  # similar to "CROSS_COMPILE" when build kernel
  linux_arm_cross_compile = ""
}

template("linux_arm_toolchain") {
  if (linux_arm_cross_compile == "") {
    assert(
        false,
        "assign a value to the build argument 'linux_arm_cross_compile' when use linux_arm_toolchain")
  }

  _invoker_toolchain_args = invoker.toolchain_args
  _linux_arm_toolchain_args = {
    current_os = invoker.current_os
    is_clang = false
    forward_variables_from(_invoker_toolchain_args, "*")
  }

  gcc_toolchain(target_name) {
    toolchain_args = _linux_arm_toolchain_args
    ar = linux_arm_cross_compile + "ar"
    cc = linux_arm_cross_compile + "gcc"
    cxx = linux_arm_cross_compile + "g++"
  }
}
